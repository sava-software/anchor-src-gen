plugins {
  alias(libs.plugins.jlink)
}

ext {
  VERSION = project.findProperty('version')
  VCS_URL = 'https://github.com/sava-software/anchor-src-gen'
}

apply(plugin: 'java-library')
apply(plugin: 'maven-publish')
apply(plugin: 'org.beryx.jlink')

project.group = 'software.sava'
project.version = "$VERSION"

final JLV = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 24)
final GPR_USER = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
final GPR_TOKEN = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")

plugins.withType(JavaPlugin).configureEach {
  java {
    modularity.inferModulePath = true
    toolchain {
      languageVersion = JLV
    }
  }
}

repositories {
  maven {
    url = "https://maven.pkg.github.com/sava-software/json-iterator"
    credentials {
      username = GPR_USER
      password = GPR_TOKEN
    }
  }
  maven {
    url = "https://maven.pkg.github.com/sava-software/sava"
    credentials {
      username = GPR_USER
      password = GPR_TOKEN
    }
  }
  maven {
    url = "https://maven.pkg.github.com/sava-software/solana-programs"
    credentials {
      username = GPR_USER
      password = GPR_TOKEN
    }
  }
  mavenCentral()
}

dependencies {
  implementation libs.bouncycastle
  implementation libs.json.iterator
  implementation libs.sava.core
  implementation libs.sava.rpc

  testImplementation libs.junit.jupiter
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    exceptionFormat = "full"
    showStandardStreams = true
  }
}

tasks.register('sourcesJar', Jar) {
  from sourceSets.main.allJava
  archiveClassifier.set('sources')
}

tasks.withType(Javadoc).configureEach {
  options.addStringOption('Xdoclint:none', '-quiet')
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      groupId = project.group
      artifactId = project.name
      version = project.version

      pom {
        name = project.name
        description = "Solana Anchor IDL Source Generator"
        url = "$VCS_URL"
        licenses {
          license {
            name = 'GPL-3.0 license'
            url = 'https://github.com/sava-software/anchor-src-gen/blob/main/LICENSE'
          }
        }
        developers {
          developer {
            name = 'Jim'
            id = 'jpe7s'
            email = 'jpe7s.salt188@passfwd.com'
            organization = 'Sava Software'
            organizationUrl = 'https://github.com/sava-software'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:sava-software/anchor-src-gen.git'
          developerConnection = 'scm:git:ssh@github.com:sava-software/anchor-src-gen.git'
          url = "$VCS_URL"
        }
      }
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/sava-software/anchor-src-gen"
      credentials {
        username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
        password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
      }
    }
  }
}

afterEvaluate {
  jlink {
    imageName.set(project.name)
    options.addAll(List.of(
        '--bind-services',
        '--no-man-pages',
        '--no-header-files',
        '--ignore-signing-information'
    ))
    enableCds()
  }
}
